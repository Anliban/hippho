
buildscript {
    ext {
        kotlin_version = '1.4.10'
        appCompat = '1.2.0'
        core = '1.3.1'
        lifecycle_version = '2.2.0'
        room_version = '2.2.5'
        dagger_version = '2.28.3-alpha'
        constraintlayout_version = '2.0.1'
        material_version = '1.2.0'
        ted_permission_version = '2.2.3'
        coroutine = '1.3.9'
        glide = '4.11.0'
        navigation = '2.3.0'
        fragment_version = '1.2.5'
        timber = '4.7.1'
        joda_time_version = '2.10.3'
        swipe = '1.1.0'
        insetter = '0.3.0'
        androidHilt = '1.0.0-alpha02'

        testRunner = '1.3.0'
        espresso = '3.3.0'
        junit = '4.13'
        coreTesting = '2.1.0'
        mockk = '1.10.0'
    }
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$dagger_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navigation"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

Plugin.metaClass.isAndroidApp = { ->
    delegate.class.getCanonicalName() == "com.android.build.gradle.AppPlugin"
}
Plugin.metaClass.isDynamicFeature = { ->
    delegate.class.getCanonicalName() == "com.android.build.gradle.DynamicFeaturePlugin"
}
Plugin.metaClass.isAndroidLibrary = { ->
    delegate.class.getCanonicalName() == "com.android.build.gradle.LibraryPlugin"
}

allprojects {
    repositories {
        google()
        jcenter()

    }

    plugins.whenPluginAdded {
        if (it.isAndroidApp() || it.isAndroidLibrary() || it.isDynamicFeature()) {
            android.compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile).all {
        kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi"]
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
